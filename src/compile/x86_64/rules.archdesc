/* Instruction selection rules for x86_64 */

code "rules_preamble_namespaced" [[
  template<typename X>
  bool isZero(const X &z) {
    return std::all_of(std::begin(z), std::end(z), [](auto x) { return x == 0; });
  }

  int log2(std::uint64_t u) {
    if ( __builtin_popcount(u) == 1 ) {
      return __builtin_ctz(u);
    } else return -1;
  }

  bool makeShiftValue(std::shared_ptr<nnc::compile::RtlType> pow2ty, std::span<std::uint8_t> &pow2,
                      int &shiftValue) {
      if ( pow2ty && ::nnc::compile::isIntType(*pow2ty) ) {
        shiftValue = log2(::nnc::compile::interpretAsInt(pow2));
        return ( shiftValue >= 0 && shiftValue <= 3 );
      } else {
        shiftValue = 0;
        return !pow2ty;
      }
  }
]];

insnrule load_zero {
  var $ty : regtype;
  var $x : constant;
  var $out : register;

//  regclass $out gp;
  if [[ isZero($x) && isIntType(*$ty) ]];

  pattern {
    const(const=($ty) $x, dest->($ty) $out);
  }

  asm {
    [[ auto in1($fn.variable("in", $ty)); ]];
    [[ $builder.arbitrary(in1, regclass::gp); ]];
    xor($out, [[in1]], [[in1]]);
  }
}

insnrule load_const_gp {
  var $x : constant;
  var $out : register;
  var $ty : regtype;

  if [[ isIntType(*$ty) ]];

  pattern {
    const(const=($ty) $x, dest->$out);
  }
  asm {
    mov($out, [[ ::nnc::arch::x86_64::types::modrmimm($x, $ty) ]]);
  }
}

insnrule load_const_fp {
  var $x : constant;
  var $out : register;
  var $ty : regtype;

  check [[
    ::nnc::compile::RtlTypeInfo info(*$ty);
    return info.isFloatType();
  ]];

  pattern {
    const(const=($ty) $x, dest->$out);
  }
  asm {
    movss($out, [[ ::nnc::arch::x86_64::types::modrmimm($x, $ty) ]]);
  }
}

insnrule float_mul {
  var $ty: regtype;
  var $c : constant;
  var $x : register;
  var $y : register;
  var $out : register;

  pattern {
   { const(const=$c, dest->$y); }?
   { mul(left=($ty)$x, right=$y, dest->$out);
   | mul(left=($ty)$y, right=$x, dest->$out); }
  }

  check [[
    ::nnc::compile::RtlTypeInfo info(*$ty);
    if ( !info.isFloatType() ) return false;

    if ( $c.data() ) {
       std::cerr << "TODO constant alloation" << std::endl;
       return false;
    }
    return info.width() == 64 || info.width() == 32;
  ]];

  asm {
    [[::nnc::compile::RtlTypeInfo info(*$ty);]];
    mulss($out, $x, $y) if [[ info.width() == 32 ]];
  }
}

insnrule sint_mul {
  var $ty : regtype;
  var $x : register;
  var $y: register;
  var $out: register;
  var $c : constant;

  pattern {
    { const(const=$c, dest->$y); }?
    { mul(left=($ty)$x, right=$y, dest->$out);
    | mul(left=($ty)$y, right=$x, dest->$out); }
  }

  if [[isIntType(*$ty) ]];
  check [[
    ::nnc::compile::RtlTypeInfo info(*$ty);
    if ( $c.data() ) {
      return info.width() <= 32;
    } else
      return true;
  ]];

  asm {
    [[::nnc::compile::RtlTypeInfo info(*$ty);]];
    imul($out, $x, [[$c.data() ? ::nnc::arch::x86_64::types::modrmimm($c, $ty) : $y]]) if [[ info.intFormat() == ::nnc::compile::RtlTwosComplement ]];
  }
};

insnrule lt_zero {
  var $ty: regtype;
  var $a: register;
  var $c : register;
  var $x : constant;
  var $out : register;

  pattern {
    const(const=$x, dest->$c);
    lt(left=$a, right=$c, dest->$out);
  }

  if "isZero($x)";

  asm {
    test($a, $a, sign=$out);
  }
}

insnrule lt {
  var $a : register;
  var $b : register;
  var $out : register;
  var $c : constant;
  var $ty : regtype;

  pattern {
    { const(const=($ty) $c, dest->$b); }?
    lt(left=$a, right=$b, dest->$out);
  }

  asm {
    cmp($a, [[ $c.data() ? ::nnc::arch::x86_64::types::modrmimm($c, $ty) : $b ]], sign=$out);
  }
}

insnrule indexed_muladd {
  var $base : register;
  var $index : register;
  var $scale : register;
  var $intptr : register;
  var $scaledIndex : register;
  var $pow2 : constant;
  var $pow2ty : regtype;
  var $ty : regtype;

  pattern {
    { const(const=($pow2ty) $pow2, dest->$scale);
      { mul(left=$scaledIndex, right=$scale, dest->$index) |
        mul(left=$scale, right=$scaledIndex, dest->$index) }
    }?

    { add(left=$base, right=$index, dest->($ty) $intptr) |
      add(left=$index, right=$base, dest->($ty) $intptr) }
  };

  extraPrivate = [[
    int shiftValue;
  ]];

  if [[ isIntType(*$ty) ]];
  if [[ makeShiftValue($pow2ty, $pow2, shiftValue) ]];

  asm {
    lea($intptr, [[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]]);
  };
}

insnrule indexed_load {
  var $base : register;
  var $index : register;
  var $scale : register;
  var $intptr : register;
  var $ptr : register;
  var $scaledIndex : register;
  var $pow2 : constant;
  var $pow2ty : regtype;
  var $out : register;
  var $ty: regtype;

  pattern {
    { const(const=($pow2ty) $pow2, dest->$scale);
      { mul(left=$scaledIndex, right=$scale, dest->$index) |
        mul(left=$scale, right=$scaledIndex, dest->$index) }
    }?

    { add(left=$base, right=$index, dest->$intptr) |
      add(left=$index, right=$base, dest->$intptr) }

    inttoptr(in=$intptr, out->$ptr);
    load(addr=$ptr, dest->($ty) $out);
  }

  extraPrivate = [[
    int shiftValue;
  ]];
  if [[ makeShiftValue($pow2ty, $pow2, shiftValue) ]];

  check [[
    ::nnc::compile::RtlTypeInfo info(*$ty);
    if ( info.isIntType() && info.width() >= 32 ) {
      if ( $index || $pow2ty ) return false;
    }
    return true;
  ]];

  asm {
    [[::nnc::compile::RtlTypeInfo info(*$ty);]];
    mov($out, [[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]]) if [[ info.isIntType() ]];
    movss($out, [[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]]) if [[ info.isFloatType() ]];
  }
}

insnrule inttoptr {
  var $intptr: register;
  var $ptr: register;

  pattern {
    inttoptr(in=$intptr, out->$ptr);
  }

  alias $intptr $ptr;
}

insnrule assign {
   var $src : register;
   var $dst : register;

   pattern {
     mov(in=$src, out=$dst);
   }

   alias $src $dst;
}

insnrule load {
  var $addr : register;
  var $ty : regtype;
  var $out : register;

  pattern {
    load(addr=$addr, dest->($ty) $out);
  }

  asm {
    [[using namespace ::nnc::arch::x86_64::types;]];
    [[using namespace ::nnc::compile;]];
    [[RtlTypeInfo info(*$ty);]];
    mov($out, [[modrm::memref($addr, $ty)]]) if [[info.isIntType()]];
    movss($out, [[modrm::memref($addr, $ty)]]) if [[info.isFloatType()]];
  }
}

insnrule store {
  var $addr : register;
  var $ty : regtype;
  var $in : register;

  pattern {
    store(addr=$addr, value=($ty) $in);
  }

  asm {
    [[using namespace ::nnc::arch::x86_64::types;]];
    [[using namespace ::nnc::compile;]];
    [[RtlTypeInfo info(*$ty);]];
    mov([[modrm::memref($addr, $ty)]], $in) if [[info.isIntType()]];
    movss([[modrm::memref($addr, $ty)]], $in) if [[info.isFloatType()]];
  }
}

insnrule indexed_store {
  var $base : register;
  var $index : register;
  var $scale : register;
  var $intptr : register;
  var $ptr : register;
  var $scaledIndex : register;
  var $pow2 : constant;
  var $pow2ty : regtype;
  var $in : register;
  var $ty: regtype;

  pattern {
    { const(const=($pow2ty) $pow2, dest->$scale);
      { mul(left=$scaledIndex, right=$scale, dest->$index) |
        mul(left=$scale, right=$scaledIndex, dest->$index) }
    }?

    { add(left=$base, right=$index, dest->$intptr) |
      add(left=$index, right=$base, dest->$intptr) }

    inttoptr(in=$intptr, out->$ptr);
    store(addr=$ptr, value=($ty) $in);
  }

  extraPrivate = [[
    int shiftValue;
  ]];
  if [[ makeShiftValue($pow2ty, $pow2, shiftValue) ]];

  asm {
    [[::nnc::compile::RtlTypeInfo info(*$ty);]];
    mov([[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]],
        [[::nnc::arch::x86_64::types::modrm($in)]]) if [[ info.isIntType() ]];
    movss([[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]],
          $in) if [[ info.isFloatType() ]];
  }
}

insnrule add_fp_32 {
  var $left : register;
  var $right : register;
  var $dest : register;

  pattern {
    add(left=$left, right=$right, dest->$dest);
  }

  check [[ ::nnc::compile::RtlTypeInfo lty(*$left->type()), rty(*$right->type());
           return (lty.isFloatType() && rty.isFloatType() && lty.width() == rty.width() && lty.width() == 32); ]];

  asm {
    addss($dest, $left, $right);
  }
}

insnrule sub {
  var $left: register;
  var $right: register;
  var $dest: register;
  var $ty: regtype;
  var $const: constant;

  pattern {
    { const(const=$const, dest->$right); }?
    sub(left=$left, right=$right, dest->($ty) $dest);
  }

  if [[isIntType(*$ty)]];

  asm {
    sub($dest, $left, $right) if [[!$const.data()]];
    sub($dest, $left, [[::nnc::arch::x86_64::types::modrmimm($const, $ty)]]) if [[$const.data()]];
  }
}

insnrule add {
  var $left: register;
  var $right: register;
  var $dest: register;
  var $ty: regtype;
  var $const: constant;

  pattern {
    { const(const=$const, dest->$right); }?
    add(left=$left, right=$right, dest->($ty) $dest);
  }

  if [[isIntType(*$ty)]];

  asm {
    add($dest, $left, $right) if [[!$const.data()]];
    add($dest, $left, [[::nnc::arch::x86_64::types::modrmimm($const, $ty)]]) if [[$const.data()]];
  }
}

insnrule dec {
  var $left : register;
  var $one : constant;
  var $onex : register;
  var $ty : regtype;
  var $dest : register;

  pattern {
    const(const=($ty) $one, dest-> $onex);
    sub(left=$left, right=$onex, dest->$dest);
  }

  check [[
    if ( isIntType(*$ty) ) {
      std::uint64_t i = ::nnc::compile::interpretAsInt($one);
      return i == 1;
    } else return false;
  ]];

  asm {
    dec($dest, $left);
  }
}

insnrule inc {
  var $left : register;
  var $one : constant;
  var $onex : register;
  var $ty : regtype;
  var $dest : register;

  pattern {
    const(const=($ty) $one, dest-> $onex);
    add(left=$left, right=$onex, dest->$dest);
  }

  check [[
    if ( isIntType(*$ty) ) {
      std::uint64_t i = ::nnc::compile::interpretAsInt($one);
      return i == 1;
    } else return false;
  ]];

  asm {
    inc($dest, $left);
  }
}

// Due to the way x86-64 handles 32-bit values, we always have the top top dword being 0
insnrule zext_32_to_64 {
  var $in : register;
  var $out : register;

  pattern {
    zext(in=$in, out->$out);
  }

  check [[
    ::nnc::compile::RtlTypeInfo inty(*$in->type()), outty(*$out->type());
    return ( inty.isIntType() && outty.isIntType() ) &&
      inty.width() == 32 && outty.width() == 64;
  ]];

  alias $in $out;
}

insnrule zext_8_16_to_32_64 {
  var $in: register;
  var $out: register;
  var $base : register;
  var $index : register;
  var $scale : register;
  var $intptr : register;
  var $scaledIndex : register;
  var $pow2 : constant;
  var $pow2ty : regtype;
  var $ty : regtype;
  var $ptr : register;

  pattern {
    {
      { const(const=($pow2ty) $pow2, dest->$scale);
        { mul(left=$scaledIndex, right=$scale, dest->$index) |
          mul(left=$scale, right=$scaledIndex, dest->$index) }
      }?

      { add(left=$base, right=$index, dest->($ty) $intptr) |
        add(left=$index, right=$base, dest->($ty) $intptr) }

      inttoptr(in=$intptr, out->$ptr);
      load(addr=$ptr, dest->$in);
    }?

    zext(in=$in, out->$out);
  }

  extraPrivate = [[
    int shiftValue;
  ]];

  if [[ $ty && isIntType(*$ty) ]];
  if [[ makeShiftValue($pow2ty, $pow2, shiftValue) ]];

  asm {
    movzx($out, [[::nnc::arch::x86_64::types::modrm::memref($base, $ty, $scaledIndex ? $scaledIndex : $index, shiftValue)]]) if [[$ptr]];
    movzx($out, $in) if [[ !($ptr) ]];
  }
}

// Ptrto int
insnrule ptr_to_int64 {
  var $ptr : register;
  var $int : register;

  pattern {
    ptrtoint(in=$ptr, out->$int);
  }

  if [[ $int->type() == fn.types().intType(::nnc::compile::RtlTwosComplement, 64) ]];

  alias $ptr $int;
}

insnrule i_to_float {
  var $src: register;
  var $dst: register;
  var $srcty: regtype;
  var $dstty: regtype;

  pattern {
    uitofp(in=($srcty) $src, out=($dstty) $dst);
  }

  // This conversion works if the integer is signed or unsigned, but for
  // unsigned integers, the width must be less than 64
  check [[
    nnc::compile::RtlTypeInfo srcinfo(*$srcty);
    assert(srcinfo.isIntType());

    if ( srcinfo.width() > 64 ) return false; // Have to zero-extend first
    if ( srcinfo.intFormat() == nnc::compile::RtlUnsigned ) {
      if ( srcinfo.width() == 64 ) return false;
    } else if ( srcinfo.intFormat() == nnc::compile::RtlOnesComplement ) return false;

    nnc::compile::RtlTypeInfo dstinfo(*$dstty);
    assert(dstinfo.isFloatType());

    if ( dstinfo.width() > 64 ) return false; // Can't do this for float 64

    return true;
  ]];

  asm {
    [[nnc::compile::RtlTypeInfo dstinfo(*$dstty);]];
    cvtsi2ss($dst, $src) if [[dstinfo.width() <= 32]];
  }
}